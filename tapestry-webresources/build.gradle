plugins {
    id 'tapestry.junit5-convention'
    id 'tapestry.testng-convention'
}

description = 'Integration with WRO4J to perform runtime CoffeeScript compilation, JavaScript minimization, and more.'

def moduleLibs = [
    closureCompiler: 'com.google.javascript:closure-compiler-unshaded:v20220502',
    less4j: 'com.github.sommeri:less4j:1.12.0',
    autoValueAnnotations: 'com.google.auto.value:auto-value-annotations:1.9',
    rhino: 'org.mozilla:rhino:1.7.7.2',
]

dependencies {
    api project(':tapestry-core')

    api moduleLibs.closureCompiler

    implementation moduleLibs.less4j

    compileOnly moduleLibs.autoValueAnnotations

    implementation moduleLibs.rhino

    testImplementation project(':tapestry-test')
    testImplementation project(':tapestry-runner')

    testImplementation(libs.geb.spock) {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all' // avoid multiple Groovy compilers on classpath
    }

    testImplementation project(":tapestry-spock")
    testImplementation libs.selenium.java
    testImplementation libs.webdrivermanager
}

test {
    systemProperties(
            'geb.build.reportsDir': "${reporting.baseDir}/geb",
            'tapestry.compiled-asset-cache-dir': "${buildDir}/compiled-asset-cache",
            'tapestry.production-mode': 'false')
}

jar {
    manifest {
        attributes 'Tapestry-Module-Classes': 'org.apache.tapestry5.webresources.modules.WebResourcesModule'
    }
}
