plugins {
    id 'tapestry.junit5-convention'
    id 'tapestry.testng-convention'
}

description = 'Integration with WRO4J to perform runtime CoffeeScript compilation, JavaScript minimization, and more.'

dependencies {
    api project(':tapestry-core')

    api libs.closure.compiler

    implementation libs.less4j
    implementation libs.rhino

    compileOnly libs.auto.value.annotations

    testImplementation project(':tapestry-test')
    testImplementation project(':tapestry-runner')

    testImplementation(libs.geb.spock) {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all' // avoid multiple Groovy compilers on classpath
    }

    testImplementation project(":tapestry-spock")
    testImplementation libs.selenium.java
    testImplementation libs.webdrivermanager
}


test {
    systemProperties(
        'geb.build.reportsDir': "$reporting.baseDir/geb",
        'tapestry.compiled-asset-cache-dir': "$buildDir/compiled-asset-cache",
        'tapestry.production-mode': 'false',
        'tapestry.compress-whitespace': 'false',
        'tapestry.combine-scripts': 'false'
    )
}

tasks.named('jar', Jar) {
    manifest {
        attributes 'Tapestry-Module-Classes': 'org.apache.tapestry5.webresources.modules.WebResourcesModule'
    }
}
