plugins {
    id 'tapestry.testng-convention'
}

import org.apache.tools.ant.filters.ReplaceTokens

description = 'Tapestry classes that handle HTTP requests'

dependencies {
    implementation project(':tapestry-ioc')
    implementation project(':tapestry-json')
    implementation project(':beanmodel')

    provided libs.javax.servlet.api

    implementation libs.commons.codec
    implementation libs.commons.io

    testImplementation project(':tapestry-test')
    testImplementation project(':tapestry-test-constants')

    testRuntimeOnly("${libs.hsqldb.get().module.group}:${libs.hsqldb.get().module.name}:${libs.hsqldb.get().version}:jdk8")
    testRuntimeOnly libs.slf4j.simple
}

// Not sure why this is necessary:
tasks.named('compileTestGroovy') {
    dependsOn compileTestJava
}

tasks.named('jar', Jar) {
    from('src/main/filtered-resources') {
        filter(ReplaceTokens, tokens: [version: project.parent.version])
    }
}
