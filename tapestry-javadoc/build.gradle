plugins {
    id 'tapestry.junit5-convention'
}

description = 'JavaDoc Plugin for Tapestry that generates component reference documentation for component classes'

dependencies {
    implementation project(':tapestry-core')

    implementation libs.commons.lang3
    implementation libs.commons.text

    testImplementation platform(libs.spock.bom)
    testImplementation libs.spock.core
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile).configureEach {
    // Override Java 8 compatibility inherited from root
    sourceCompatibility = '11'
    targetCompatibility = '11'

    // Only apply --add-modules if target is not 1.8
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
        options.compilerArgs += [
            '--add-modules',
            'jdk.compiler'
        ]
    }
}
