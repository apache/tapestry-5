plugins {
    id 'tapestry.testng-convention'
}

description = 'Provides support for simple CRUD applications built on top of Tapestry and JPA'

def moduleLibs = [
    jakartaCdiApi: 'jakarta.enterprise:jakarta.enterprise.cdi-api:3.0.1',
    eclipselink: 'org.eclipse.persistence:eclipselink:3.0.4',
    dbcp: 'org.apache.tomcat:dbcp:6.0.32',
    h2: 'com.h2database:h2:1.3.175'
]

dependencies {
    implementation project(':tapestry-core')

    api libs.jakarta.persistence.api
    implementation moduleLibs.jakartaCdiApi

    testImplementation project(':tapestry-test')
    testImplementation moduleLibs.eclipselink

    testRuntimeOnly moduleLibs.dbcp
    testRuntimeOnly moduleLibs.h2
    
    testImplementation libs.testng
}

(1..6).each { i ->
    tasks.register("runTestApp$i", JavaExec) {
        description = "Start app$i integration test app, useful when debugging failing integration tests"
        mainClass = 'org.apache.tapestry5.test.JettyRunner'
        args '-d', "src/test/app$i", '-p', '8080'
        classpath = project.sourceSets.test.runtimeClasspath
    }
}

jar {
    manifest {
        attributes 'Tapestry-Module-Classes': 'org.apache.tapestry5.jpa.modules.JpaModule'
    }
}
