plugins {
    id 'tapestry.testng-convention'
}

description = 'Provides support for simple CRUD applications built on top of Tapestry and JPA'

dependencies {
    implementation project(':tapestry-core')

    api libs.jakarta.persistence.api
    implementation libs.jakarta.cdi.api

    testImplementation project(':tapestry-test')
    testImplementation libs.eclipselink

    testRuntimeOnly libs.dbcp
    testRuntimeOnly libs.h2
}

(1..6).each {
    tasks.register("runTestApp${it}", JavaExec) {
        description = "Start app${it} integration test app, useful when debugging failing integration tests"
        mainClass = 'org.apache.tapestry5.test.JettyRunner'
        args '-d', "src/test/app${it}", '-p', '8080'
        classpath = project.sourceSets.test.runtimeClasspath
    }
}

tasks.named('jar', Jar) {
    manifest {
        attributes 'Tapestry-Module-Classes': 'org.apache.tapestry5.jpa.modules.JpaModule'
    }
}
