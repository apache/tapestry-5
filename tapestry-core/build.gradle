plugins {
    id 'tapestry.junit5-convention'
    id 'tapestry.testng-convention'
}

import t5build.TapestryBuildLogic
import org.apache.tools.ant.filters.ReplaceTokens

description = 'Central module for Tapestry, containing all core services and components'

def mainGeneratedDir = 'src/main/generated'
def testGeneratedDir = 'src/test/generated'

dependencies {
    api project(':tapestry-ioc')
    api project(':tapestry-json')
    api project(':beanmodel')
    api project(':tapestry-http')

    implementation libs.jakarta.annotation.api
    implementation libs.jakarta.xml.bind.api

    implementation libs.commons.codec
    implementation libs.commons.lang3

    provided project(':tapestry-test')
    provided project(':tapestry-test-constants')

    provided libs.jakarta.servlet.api

    testImplementation libs.httpcomponents.httpclient
    testImplementation project(":tapestry-spock")

    testRuntimeOnly("${libs.hsqldb.get().module.group}:${libs.hsqldb.get().module.name}:${libs.hsqldb.get().version}:jdk8")
    testRuntimeOnly libs.guice
}

def npmWorkingDir = 'src/main/typescript/'
def npmExec = TapestryBuildLogic.isWindows() ? 'npm.cmd' : 'npm'

tasks.register('npmInstall', Exec) {
    group 'TypeScript'
    description "Runs npm install"
    
    workingDir = npmWorkingDir
    commandLine npmExec, 'install'
}

tasks.register('compileTypeScriptToAmd', Exec) {
    group 'TypeScript'

    dependsOn npmInstall

    workingDir = npmWorkingDir
    commandLine npmExec, 'run', 'build-amd'
}

tasks.register('compileTypeScriptToEsModule', Exec) {
    group 'TypeScript'

    dependsOn npmInstall
    
    workingDir = npmWorkingDir
    commandLine npmExec, 'run', 'build-es-module'
}

tasks.register('compileTypeScript', Delete) {
    group = 'TypeScript'

    dependsOn(compileTypeScriptToAmd)
    dependsOn(compileTypeScriptToEsModule)
}

tasks.register('generateTypeScriptDocs', Exec) {
    group 'TypeScript'

    dependsOn npmInstall
    
    workingDir = npmWorkingDir
    commandLine npmExec, 'run', 'docs'
}

tasks.register('cleanTypeScriptFiles', Delete) {
    delete fileTree("src/main/resources/META-INF/assets/es-modules/t5/core") {
        include '**.js'
    }
    delete fileTree("src/main/resources/META-INF/modules/t5/core") {
        include '**.js'
    }
    delete "src/main/typescript/docs"
}

processResources.dependsOn compileTypeScript
clean.dependsOn cleanTypeScriptFiles

// Not sure why this is necessary:
compileTestGroovy.dependsOn compileTestJava

test {
    // Needed to have XMLTokenStreamTests.testStreamEncoding() passing on Java 9+
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_9)) {
        jvmArgs('--add-opens=java.base/java.nio.charset=ALL-UNNAMED')
    }

    // TAP5-2722
    systemProperty 'user.language', 'en'
}

[
    'app1',
    'app2',
    'app3',
    'app4',
    'app5',
    'app7',
    'appfolder'
].each { appName ->
    tasks.register("runTest${appName.capitalize()}", JavaExec) {
        group = 'Application'
        description = "Starts the ${appName} integration test app, useful when debugging."
        mainClass = 'org.apache.tapestry5.test.JettyRunner'
        args '-d', "src/test/${appName}", '-p', '8080'
        classpath = sourceSets.test.runtimeClasspath
    }
}

// Default is jQuery and Require.js enabled, so here are the tests for the
// other combinations

task testWithJqueryAndRequireJsDisabled(type: Test) {
    systemProperties."tapestry.javascript-infrastructure-provider" = 'jquery'
    systemProperties."tapestry.require-js-enabled" = 'false'
}

task testWithPrototypeAndRequireJsEnabled(type: Test) {
    systemProperties."tapestry.javascript-infrastructure-provider" = 'prototype'
    systemProperties."tapestry.require-js-enabled" = 'true'
}  

task testWithPrototypeAndRequireJsDisabled(type: Test) {
    systemProperties."tapestry.javascript-infrastructure-provider" = 'prototype'
    systemProperties."tapestry.require-js-enabled" = 'false'
}
