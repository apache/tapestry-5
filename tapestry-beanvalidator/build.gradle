plugins {
    id 'tapestry.testng-convention'
}

import t5build.*

description = 'Support for JSR-303 Bean Validation via the Hibernate validator implementation'

def moduleLibs = [
    hibernateValidator: 'org.hibernate:hibernate-validator:8.0.1.Final',
]

dependencies {
    implementation project(':tapestry-core')

    implementation libs.jakarta.validation.api
    implementation moduleLibs.hibernateValidator

    implementation libs.selenium.legrc, {
        exclude group: "org.seleniumhq.selenium", module: "jetty-repacked"
        exclude group: "org.testng", module: "testng"
    }

    testImplementation project(':tapestry-test')
}

def generatedOutputBaseDir = 'src/main/generated'

task compileCoffeeScript(type: CompileCoffeeScript) {
    outputDir = "${generatedOutputBaseDir}/compiled-coffeescript"
}

// Start up the test app, useful when debugging failing integration tests
task runTestApp303(type:JavaExec) {
    mainClass = 'org.apache.tapestry5.test.JettyRunner'
    args "-d", "src/test/webapp", "-p", "8080"
    classpath += project.sourceSets.test.runtimeClasspath
}

clean.delete generatedOutputBaseDir

sourceSets {
    main {
        output.dir(compileCoffeeScript.outputDir, builtBy: compileCoffeeScript)
    }
}

jar.manifest.attributes 'Tapestry-Module-Classes': 'org.apache.tapestry5.beanvalidator.modules.BeanValidatorModule'
