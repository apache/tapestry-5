plugins {
    id 'tapestry.junit4-legacy-convention'
}

description = 'JCache (JSR 107) integration for Tapestry-IoC'

dependencies {
    provided project(":tapestry-ioc")

    implementation libs.javax.cache.api
    implementation libs.cache.annotations

    testImplementation libs.javax.cache.tests
    testImplementation libs.javax.cache.test.domain
    testRuntimeOnly libs.infinispan.jcache /* Just to be able to run the tests */
}

tasks.named('jar', Jar) {
    manifest {
        attributes 'Tapestry-Module-Classes': 'org.apache.tapestry5.jcache.module.JCacheModule'
    }
}

test {
    if (JavaVersion.current().isJava9Compatible()) {
        jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
        jvmArgs '--add-opens', 'java.base/java.util.concurrent=ALL-UNNAMED'
        jvmArgs '--add-opens', 'java.base/java.io=ALL-UNNAMED'
        jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
        jvmArgs '--add-opens', 'java.base/java.lang.invoke=ALL-UNNAMED'
        jvmArgs '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED'
    }
}
