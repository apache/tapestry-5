plugins {
    id 'tapestry.testng-convention'
}

description = 'Integration of Tapestry with the Spring Inversion Of Control Container'

// Spring 6 requires Java 17
sourceCompatibility = '17'
targetCompatibility = '17'

def moduleLibs = [
    springWebsocket: 'org.springframework:spring-websocket:6.0.11',
]

dependencies {
    implementation project(':tapestry-core')
    implementation project(':tapestry-ioc')

    implementation moduleLibs.springWebsocket

    provided libs.jakarta.servlet.api

    testImplementation project(':tapestry-test')
}

['', '1'].each { suffix ->
    tasks.register("runTestApp${suffix}", JavaExec) {
        group = 'Application'
        description = "Start tapestry-spring integration test app${suffix}, useful when debugging failing integration tests"
        mainClass = 'org.apache.tapestry5.test.JettyRunner'
        args '-d', "src/test/webapp${suffix}", '-p', '8080'
        classpath += project.sourceSets.test.runtimeClasspath
    }
}

tasks.named('jar', Jar) {
    manifest {
        attributes 'Tapestry-Module-Classes': 'org.apache.tapestry5.spring.modules.SpringModule'
    }
}
